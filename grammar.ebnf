Module = { Declaration }, ? EOI ?;


Declaration = StructDecl | FunctionDef;


FunctionDef = "func", Identifier, "(",  FuncDefParamList, ")", "{",  { Statement }, "}";


FuncDefParamList = [ParamSet, { ",", ParamSet }];

ParamSet = ParamIdentifierList, ":", Type;

ParamIdentifierList = Identifier, { ",", Identifier };


StructDecl = "struct", Identifier, "{", { StructField }, "}";


StructField = Identifier, ":", Type, ";";


Statement = "{", { Statement }, "}"
          | "while", "(", Expression, ")", Statement
          | Identifier, "=", Expression, ";"
          | Identifier, "[", Expression, "]", "=", Expression, ";"
          | "if", "(", Expression, ")", Statement;


Type = "int"
     | "int", "[", "]" 
     | "bool"
     | Identifier;


Identifier = ? IDENTIFIER ?;
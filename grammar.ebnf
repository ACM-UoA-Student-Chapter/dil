Module = { Declaration }, ? EOI ?;


Declaration = StructDecl | FunctionDef;


FunctionDef = "func", Identifier, "(",  FuncDefParamList, ")", "{",  { Statement }, "}";


FuncDefParamList = [ParamSet, { ",", ParamSet }];

ParamSet = ParamIdentifierList, ":", Type;

ParamIdentifierList = Identifier, { ",", Identifier };


StructDecl = "struct", Identifier, "{", { StructField }, "}";


StructField = Identifier, ":", Type, ";";


Statement = "{", { Statement }, "}"
          | Type, Identifier, ";"
          | "while", "(", Expression, ")", "{", { Statement }, "}"
          | Lval, "=", Expression, ";"
          | Lval, "[", Expression, "]", "=", Expression, ";"
          | "if", "(", Expression, ")", Statement;


Lval = Identifier, { ".", Identifier };


Type = "int"
     | "int", "[", "]" 
     | "bool"
     | Identifier;


Identifier = ? IDENTIFIER ?;